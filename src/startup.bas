100  TEXT : NORMAL : HOME
150  D$ =  CHR$(4)
200  PRINT D$;"PR#3" : REM ENABLE 80 COL
250  PRINT  CHR$(12); : REM MOVE CURSOR TO UPPER LEFT AND CLEAR WINDOW
300  POKE 49239,0: REM TURN ON HIRES

400  REM =====
500  REM VARS:
600  REM =====
700  DB% = 0: REM Debug Print (1) or Release mode (0)
800  MB% = 0: REM should we use MOUSE Binary (1) or Basic (0)
900  ME% = 0: REM is Mouse Enabled

1000  IF PEEK(50188) = 32 AND PEEK(50427) = 214 THEN ME% = 1
1100  PRINT "[D]ebug or [R]elease"
1200  GET A$
1300  IF A$ = CHR$ (68) THEN DB% = 1
1400  HOME
1500  IF ME% = 1 AND DB% = 1 THEN PRINT "Found Mouse in SLOT #4"
1600  IF ME% = 0 THEN PRINT "MOUSE NOT FOUND!"
1700  IF ME% = 0 THEN PRINT "Demo will not be functional."
1800  IF ME% = 0 THEN PRINT CHR$ (7)
1900  REM IF ME% = 0 THEN GOTO 25000

2000  IF DB% = 1 THEN PRINT "Loading title image..."
2100  IF DB% = 1 THEN PRINT "->TITLE (MAIN)"
2200  POKE 49237,0: PRINT D$;"BLOAD TITLE, A$2000, L$2000"
2300  IF DB% = 1 THEN PRINT "->TITLE (AUX)"
2400  POKE 49236,0: PRINT D$;"BLOAD TITLE, A$2000, L$2000, B$2000"
2500  POKE 49237,0
2600  IF DB% = 0 THEN GOSUB 30000 : REM Turn Graphics ON when not debugging

2700  REM INITIAL BINARY LOAD
2800  IF DB% = 1 THEN PRINT "Loading modules..."
2900  IF DB% = 1 THEN PRINT "->FDRAW.FAST"
3000  PRINT D$"BLOAD FDRAW.FAST"
3100  IF DB% = 1 THEN PRINT "->SOUND #128"
3200  PRINT D$"BLOAD SOUND"
3300  IF DB% = 1 THEN POKE 32767, 128 : REM SET SOUND ARG
3400  IF DB% = 1 THEN CALL 32512 : REM PLAY SOUND
3500  IF MB% = 0 THEN GOTO 4200

3600  IF ME% = 1 AND DB% = 1 THEN PRINT "->MOUSE"
3700  IF ME% = 1 THEN PRINT D$"BLOAD MOUSE"

3800  IF DB% = 1 THEN PRINT "Initialization..."
3900  IF DB% = 1 THEN PRINT "=>FDRAW"
4000  CALL 24576 : REM INIT FDRAW LIBRARY

4100  GOTO 4700

4200  IF DB% = 1 THEN PRINT "Binary execution..."
4300  IF DB% = 1 THEN PRINT "=>AMPERFDRAW"
4400  PRINT D$"BRUN AMPERFDRAW"
4500  IF DB% = 1 THEN PRINT "=>FDRAW"
4600  &  NEW : REM INIT FDRAW LIBRARY

4700  IF DB% = 0 THEN GOTO 5100
4800  PRINT "Ready to start.\n                 Press Any Key..."
4900  GET A$
5000  REM IF A$ = CHR$ (13) THEN TEXT
5100  IF DB% = 1 THEN GOSUB 30000 : REM Turn Graphics ON when debugging

5200  IF ME% = 0 THEN GOTO 25000
5300  IF MB% = 1 THEN GOTO 24000

10000  REM BASIC MOUSE IMPLEMENTATION
20000  CR% = 1 : CC% = 1 : BA% = 0 : BB% = 0 : REM  Setup Initial pointer position
22030  GOSUB 34000: REM  Turn Mouse ON

22040  REM V = PEEK(49152)
22042  REM IF V = 141 THEN GOTO 22220
22043  REM PRINT V
22045  REM THEN GOTO 22040 : REM Repeat

22050  GOSUB 36000 : REM  Goto BASIC Follow Mouse with AMPERFDRAW plotting
22060  GOTO 22050 : REM repeat

22230  GOSUB 35000 : REM  Turn Mouse OFF
22250  GOTO 25000

24000  IF DB% = 1 THEN PRINT "=>MOUSE"
24100  CALL 32768 : REM Call Mouse binary

25000  GET A$
25100  IF A$ = CHR$ (13) THEN TEXT 
25200  REM TEXT
25300  END

30000  POKE 49232,0: REM TURN ON GRAPHICS
30020  POKE 49235,0: REM TURN ON FULLSCREEN /49234 FULLSCREEN, 49235 MIXED/
30030  POKE 49246,0: REM TURN ON DHR
30050  RETURN

34000  PRINT : PRINT D$;"PR#4": PRINT  CHR$ (1): REM  Turn mouse on
34100  PRINT : PRINT D$;"PR#0": REM  Switch back to screen output
34200  PRINT : PRINT D$;"IN#4": REM   Get input from mouse
34250  PRINT "MOUSE ON"
34300  RETURN 

35000  PRINT : PRINT D$;"IN#0": REM  Turn mouse off,  switch back keyboard input
35100  PRINT : PRINT D$;"PR#4": PRINT  CHR$ (0): REM  Turn mouse off
35200  PRINT : PRINT D$;"PR#0": REM  switch back to screen output
35250  PRINT "MOUSE OFF"
35300  POKE 49168,0: REM  clear keyboard buffer, effectively discarding any key presses that might be stored
35400  RETURN 

36000  IF CC% = PC% AND CR% = PR% THEN  GOTO 37000 : REM Begin BASIC Mouse Follow
36500  &  HCOLOR = 3 : REM RND (1) * 6 + 1 : REM Set Random color
36999  REM  Set Mouse coordinates
37000  PC% = CC% : REM  Previous pointer position = current position
37300  PR% = CR% : REM  Previous pointer position = current position
37400  INPUT CC%,CR%,BA%: REM  Read mouse position and status
37500  CC% = CC% / 3.7: REM coordinates conversion 1024 to less than 280
37600  CR% = CR% / 5.4: REM coordinates conversion 1024 to less than 192
37700  XL = CC%
37800  YT = CR%
37900  REM PRINT XL : PRINT YT
38000  &  XDRAW XL,YT,XL + 2,YT + 2 : REM Plot Cursor (NO REDRAWING TODO)
39000  RETURN 
