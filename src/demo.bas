10  REM DEMO - Try Mouse Utilization in TEXT and HGR modes
20  REM Note: 80 COL / DHGR not yet complete.

100  TEXT : NORMAL : HOME
150  D$ = CHR$(4)

500  REM VARS:
700  DB% = 1 : REM Debug Print (1) or Release mode (0)
750  CB% = 0 : REM 80 Columns (1) or 40 Columns (0)
800  MO% = 0 : REM Mode (0: Mixed, 1: Text, 3: Graphics)
900  ME% = 0 : REM is Mouse Enabled
950  SL% = 0

999  REM CHECK $C40C AND $C4FB FOR MOUSE
1000  IF PEEK(49420) = 32 AND PEEK(49659) = 214 THEN ME% = 1
1005  IF PEEK(49676) = 32 AND PEEK(49915) = 214 THEN ME% = 2
1010  IF PEEK(49932) = 32 AND PEEK(50171) = 214 THEN ME% = 3
1015  IF PEEK(50188) = 32 AND PEEK(50427) = 214 THEN ME% = 4
1020  IF PEEK(50444) = 32 AND PEEK(50683) = 214 THEN ME% = 5
1025  IF PEEK(50700) = 32 AND PEEK(50939) = 214 THEN ME% = 6
1030  IF PEEK(50956) = 32 AND PEEK(51195) = 214 THEN ME% = 7
1035  IF PEEK(51212) = 32 AND PEEK(51451) = 214 THEN ME% = 8
1040  IF ME% > 0 THEN PRINT "Found Mouse in SLOT #"; ME% : PRINT
1050  IF ME% = 0 THEN PRINT "MOUSE NOT FOUND!" : PRINT CHR$ (7) : END
1100  IF DB% = 0 THEN 1500
1170  VTAB (2)
1190  PRINT : PRINT "Select Mode:" : PRINT "==========="
1200  IF SL% = 0 THEN INVERSE
1210  PRINT "[1] DEBUG (40 COL)"
1220  NORMAL
1230  IF SL% = 1 THEN INVERSE
1240  PRINT "[2] DEBUG (80 COL) WIP"
1250  NORMAL
1260  IF SL% = 2 THEN INVERSE
1270  PRINT "[3] RELEASE (40 COL)"
1280  NORMAL
1290  IF SL% = 3 THEN INVERSE
1300  PRINT "[4] RELEASE (80 COL) WIP"
1310  NORMAL
1320  PRINT : PRINT "[SPC/ARROWS] to Change Mode"
1330  PRINT "[1-4/ENTER] to Select Mode" : PRINT "[ESC/Q] to Quit"
1340  PRINT : PRINT "Your Choice:";
1350  GET A$
1360  K = ASC(A$)
1370  IF K = 13 THEN GOSUB 1450 : GOTO 1500
1375  IF K = 27 OR K = 81 OR K = 113 OR K = 209 THEN END
1380  IF K = 32 OR K = 21 OR K = 10 THEN SL% = SL% + 1 : IF SL% > 3 THEN SL% = 0 : GOSUB 1450 : GOTO 1170
1390  IF K = 8 OR K = 11 THEN SL% = SL% - 1 : IF SL% < 0 THEN SL% = 3 : GOSUB 1450 : GOTO 1170
1400  IF K = 49 THEN SL% = 0 : GOSUB 1450 : GOTO 1500
1410  IF K = 50 THEN SL% = 1 : GOSUB 1450 : GOTO 1500
1420  IF K = 51 THEN SL% = 2 : GOSUB 1450 : GOTO 1500
1430  IF K = 52 THEN SL% = 3 : GOSUB 1450 : GOTO 1500
1440  GOTO 1170 
1450  IF SL% = 0 THEN DB% = 1 : CB% = 0
1460  IF SL% = 1 THEN DB% = 1 : CB% = 1
1470  IF SL% = 2 THEN DB% = 0 : CB% = 0
1480  IF SL% = 3 THEN DB% = 0 : CB% = 1
1490  RETURN

1500  IF CB% = 1 THEN PRINT D$;"PR#3"
1550  HOME : IF DB% = 0 THEN PRINT "Loading..." : GOTO 1700
1560  PRINT "Debug in 40 Col, HGR" : PRINT
1600  IF CB% = 1 THEN PRINT CHR$(12); : REM CLEAR SCREEN
1700  POKE 49239,0 : REM TURN ON HIRES
1800  REM IF CB% = 1 THEN POKE 49237,0

1900  IF CB% = 0 THEN GOTO 2400

2000  IF DB% = 1 THEN PRINT "Debug in 80 Col, DHGR, Loading..." : PRINT : PRINT "->TITLE.DHGR (MAIN $2000)"
2050  POKE 49237,0 : REM Switch to MAIN HGR page
2100  PRINT D$;"BLOAD TITLE.DHGR, A$2000, L$2000"
2200  IF DB% = 1 THEN PRINT "->TITLE.DHGR (AUX $2000-$3FFF)"
2250  POKE 49236,0 : REM Switch to AUX
2300  PRINT D$;"BLOAD TITLE.DHGR, A$2000, L$2000, B$2000"
2350  POKE 49237,0 : REM Switch back to MAIN
2380  GOTO 2900

2400  IF DB% = 1 THEN PRINT "->TITLE.HGR (MAIN $2000-$3FFF)"
2500  PRINT D$;"BLOAD TITLE.HGR, A$2000, L$2000"

2600  IF DB% = 1 THEN PRINT "->SPRITES.HGR (MAIN $4000-$5FFF)"
2700  PRINT D$"BLOAD SPRITES.HGR,A$4000"

2900  IF DB% = 1 THEN PRINT "->BLITLIB (MAIN $6000-$66FF)"
3000  PRINT D$"BLOAD BLITLIB,A$6000"
3020    rem IF DB% = 1 THEN PRINT "->PRESHIFT (MAIN $6700)"
3050    rem PRINT D$"BLOAD PRESHIFT,A$6700"

4000  IF DB% = 1 THEN PRINT "->MOUSE (MAIN $6700-$6FFF)"
4100  PRINT D$"BLOAD MOUSE,A$6700"

4700  IF DB% = 0 THEN GOTO 5100
4720  VTAB(7) : PRINT : PRINT "Available Modes:" : PRINT "==============="
4740  IF MO% = 0 THEN INVERSE
4750  PRINT "[1] MIXED MODE"
4760  NORMAL
4770  IF MO% = 1 THEN INVERSE
4780  PRINT "[2] TEXT ONLY"
4790  NORMAL
4800  IF MO% = 2 THEN INVERSE
4810  PRINT "[3] GRAPHICS ONLY"
4820  NORMAL
4830  PRINT
4840  PRINT "[SPC/ARROWS/1-3/F] to Change Mode"
4850  PRINT "[Enter/ESC] to Exit"
4860  PRINT : PRINT "Your Choice:";
4870  GET A$
4880  K = ASC(A$)
4890  IF K = 13 THEN GOTO 5100
4900  IF K = 70 OR K = 32 OR K = 21 OR K = 10 THEN MO% = MO% + 1 : IF MO% > 2 THEN MO% = 0 : GOTO 4720
4905  IF K = 8 OR K = 11 THEN MO% = MO% - 1 : IF MO% < 0 THEN MO% = 2 : GOTO 4720
4910  IF K = 49 THEN MO% = 0 : GOTO 4720
4920  IF K = 50 THEN MO% = 1 : GOTO 4720
4930  IF K = 51 THEN MO% = 2 : GOTO 4720
4940  IF K = 27 THEN END
4950  GOTO 4720

5100  IF MO% = 0 OR MO% = 2 THEN POKE 49232,0 : REM Turn Graphics ON
5110  IF MO% = 2 THEN POKE 49234,0 : REM Turn ON FULLSCREEN
5120  IF MO% = 0 THEN POKE 49235,0 : REM Turn ON MIXED MODE
5130  IF CB% = 1 AND MO% = 2 OR CB% = 1 AND MO% = 0 THEN POKE 49246,0: REM Turn DHR ON

6000  CALL 26368 : REM Call Mouse binary at $6700
